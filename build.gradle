import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'org.jetbrains.kotlin.jvm'

    // (1)
    id 'net.corda.plugins.cordapp-cpb'
}

group 'org.example'
version '1.0-SNAPSHOT'

def javaVersion = VERSION_11

// (2)
cordapp {
    targetPlatformVersion platformVersion as Integer
    minimumPlatformVersion platformVersion as Integer
    workflow {
        name "ModuleNameHere"
        versionId 1
        vendor "VendorNameHere"
    }
}


// (3)
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        allWarningsAsErrors = true

        // Specify the version of Kotlin that we are using
        languageVersion = '1.6'

        // 
        apiVersion = '1.6'

        // Specify the version of Java to target
        jvmTarget = javaVersion

        // 
        freeCompilerArgs += [
                "-java-parameters",
                "-Xjvm-default=all"
        ]
    }
}

// (4)
repositories {

    // Needed for kotlin osgi bundles generated by us.
    // Will be published to MavenCentral for when we do the GA release
    maven {
        url = "$artifactoryContextUrl/corda-dependencies"
    }

    // Needed for current C5 binaries.
    // These will be published to MavenCentral when we do the GA release
    maven {
        url = "$artifactoryContextUrl/corda-os-maven"
        credentials {
            username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
            password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
        }
    }

    // C5 Binaries should go here.
    mavenCentral()
}

// (5)
dependencies {
    // We need to use kotlin stdlib built as an OSGi Bundle
    cordaProvided 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    cordaProvided platform("net.corda:corda-api:$cordaApiVersion")

    // If using transistive dependencies this will provide most of Corda-API: 
    // cordaProvided 'net.corda:corda-application'

    // Alternatively we can explicitly specify all our Corda-API dependencies:
    cordaProvided 'net.corda:corda-base'
    cordaProvided 'net.corda:corda-application'
    cordaProvided 'net.corda:corda-crypto'
    cordaProvided 'net.corda:corda-membership'
    cordaProvided 'net.corda:corda-persistence'
    cordaProvided 'net.corda:corda-serialization'

    // Not yet implemented
    // cordaProvided 'net.corda:corda-ledger'

    // If Corda-API uses a dependency it will provide it to the CorDapp.
    // The build will not work if define the same dependency as regular dependency instead.
    cordaProvided 'org.slf4j:slf4j-api'
}

test {
    useJUnitPlatform()
}
