import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'org.jetbrains.kotlin.jvm'

    // (1)
    id 'net.corda.plugins.cordapp-cpb'
}

group 'org.example'
version '1.0-SNAPSHOT'

def javaVersion = VERSION_11

// (2)
cordapp {
    targetPlatformVersion platformVersion as Integer
    minimumPlatformVersion platformVersion as Integer
    workflow {
        name "Calculator"
        versionId 1
        vendor "R3"
    }
}


// (3)
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        allWarningsAsErrors = true
        languageVersion = '1.6'
        apiVersion = '1.6'
        jvmTarget = javaVersion
        javaParameters = true   // Useful for reflection.
        freeCompilerArgs += [
                "-java-parameters",
                "-Xjvm-default=all"
        ]
    }
}

// (4)
repositories {

    /*
    // Version for using a local build.

    mavenLocal()
    mavenCentral()

    // Needed for kotlin osgi bundles for corda
    maven {
        url = "$artifactoryContextUrl/corda-dependencies"
    }
     */


    // TODO: you really need to use the cache, otherwise no complaining when upstream repos throttle us!
    // It would make sense to follow the template!

    // For Quasar SNAPSHOT
    maven {
        url = "$artifactoryContextUrl/corda-dependencies-dev"
        content {
            includeGroup 'co.paralleluniverse'
        }
    }
    //needed for kotlin osgi bundles generated by us
    maven {
        url = "$artifactoryContextUrl/corda-dependencies"
    }
    //needed for C5 binaries
    maven {
        url = "$artifactoryContextUrl/corda-os-maven"
        credentials {
            username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
            password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
        }
    }

    // needed for plugin-host dependencies
    maven {
        url = "$artifactoryContextUrl/engineering-tools-maven-unstable-local"
        credentials {
            username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
            password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
        }
    }

    def cordaUseCache = System.getenv("CORDA_USE_CACHE")
    if (cordaUseCache != null) {
        maven {
            url = "$artifactoryContextUrl/$cordaUseCache"
            name = "R3 Maven remote repositories"
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
                password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
            }
        }
    } else {
        mavenCentral()

        exclusiveContent {
            forRepository {
                maven {
                    url 'https://repository.mulesoft.org/nexus/content/repositories/public'
                }
            }
            filter {
                includeGroup 'org.mule.distributions'
                includeGroup 'antlr'
            }
        }
    }


}

// (5)
dependencies {
    // We need to use kotlin stdlib built as an OSGi Bundle
    cordaProvided 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    cordaProvided platform("net.corda:corda-api:$cordaApiVersion")
    cordaProvided 'net.corda:corda-base'
    cordaProvided 'net.corda:corda-application'
    /*
    cordaProvided 'net.corda:corda-cipher-suite'
    cordaProvided 'net.corda:corda-crypto'
    cordaProvided 'net.corda:corda-ledger'
    cordaProvided 'net.corda:corda-membership'
    cordaProvided 'net.corda:corda-persistence'
    cordaProvided 'net.corda:corda-serialization'
    cordaProvided 'net.corda:corda-avro-schema'
    cordaProvided 'net.corda:corda-config-schema'
    cordaProvided 'net.corda:corda-db-schema'
    cordaProvided 'net.corda:corda-topic-schema'

     */
    cordaProvided 'org.slf4j:slf4j-api'
}

test {
    useJUnitPlatform()
}
