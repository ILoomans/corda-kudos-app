import com.r3.csde.TestJavaClass
import com.r3.csde.CordaLifeCycleHelper
import com.r3.csde.ProjectContext
import com.r3.csde.DeployCordappHelper
import com.r3.csde.BuildCPIHelper
import com.r3.csde.ProjectUtils

plugins {
    id 'java-library'
    id 'groovy'
    id 'java'
}

configurations {
    combinedWorker{
        canBeConsumed = false
        canBeResolved= true
    }

    myPostgresJDBC {
        canBeConsumed = false
        canBeResolved = true
    }

    notaryServerCPB {
        canBeConsumed = false
        canBeResolved = true
    }
}

// Dependencies for supporting tools
dependencies {
    combinedWorker "net.corda:corda-combined-worker:$combinedWorkerVersion"
    myPostgresJDBC 'org.postgresql:postgresql:42.4.1'
    notaryServerCPB("com.r3.corda.notary.plugin.nonvalidating:notary-plugin-non-validating-server:$cordaNotaryPluginsVersion") {
        artifact {
            classifier = 'package'
            extension = 'cpb'
        }
    }

    implementation "org.codehaus.groovy:groovy-json:3.0.9"
}


def pluginGroupName = "CSDE"
def pluginImplGroupName = "other"
def cordaBinDir = System.getenv("CSDE_CORDA_BIN") ?: System.getProperty('user.home') + "/.corda/corda5"
def cordaCliBinDir =  System.getenv("CSDE_CORDA_CLI") ?:System.getProperty('user.home') + "/.corda/cli"
def cordaJDBCDir = cordaBinDir + "/jdbcDrivers"
def cordaNotaryServerDir = cordaBinDir + "/notaryserver"
def signingCertAlias="gradle-plugin-default-key"
// Get error if this is not a autotyped object
// def signingCertFName = "$rootDir/config/gradle-plugin-default-key.pem"
def signingCertFName = rootDir.toString() + "/config/gradle-plugin-default-key.pem"
def keystoreAlias = "my-signing-key"
def keystoreFName = devEnvWorkspace + "/signingkeys.pfx"
def keystoreCertFName = devEnvWorkspace + "/signingkey1.pem"
def combiWorkerPidCacheFile = devEnvWorkspace + "/CordaPID.dat"


// Need to read things from cordapp plugin
def appCpiName =  'cpi name'
def notaryCpiName =  'CSDE Notary Server CPI'

def projectContext = new ProjectContext(project,
        cordaClusterURL.toString(),
        cordaRpcUser,
        cordaRpcPasswd,
        devEnvWorkspace,
        new String("${System.getProperty("java.home")}/bin"),
        dbContainerName,
        cordaJDBCDir,
        combiWorkerPidCacheFile
)

def projectUtils = new ProjectUtils()

// todo: Start here -> Can the one off tasks be split into a separate file/ section, eg create group policy etc..

// todo: should the x500Config be loaded into the ProjectContext, then read from it wherever needed, eg in
// createGroupPolicy + 3 times in the Java

// todo investigate adding corda-cli to the class path then executing it directly - might not work as
// gradle has to set up the jar file, so its not their when you start.

// Todo: write a test flow runner helper function
// todo: Write a final output message which give the set up VNodes
// todo: question, is it possible to invoke the cli from java rather than gradle/ groovy




def deployCordappHelper = new DeployCordappHelper(projectContext)

def testJavaClass = new TestJavaClass()



tasks.register("testJava") {
    group = pluginGroupName
    doLast {
        println(testJavaClass.helloWorld())
    }
}

// CordaLifeCycle tasks

def cordaLifeCycle = new CordaLifeCycleHelper(projectContext)

tasks.register("startCorda") {
    group = pluginGroupName
    dependsOn('getDevCordaLite', 'getPostgresJDBC')
    doLast {
        mkdir devEnvWorkspace
        cordaLifeCycle.startCorda()
    }
}

tasks.register("stopCorda") {
    group = pluginGroupName
    doLast {
        cordaLifeCycle.stopCorda()
    }
}

tasks.register("getPostgresJDBC") {
    group = pluginImplGroupName
    doLast {
        copy {
            from configurations.myPostgresJDBC
            into "$cordaJDBCDir"
        }
    }
}

tasks.register("getDevCordaLite", Copy) {
    group = pluginImplGroupName
    from configurations.combinedWorker
    into cordaBinDir
}



// CordaStatusQueries

tasks.register('listVNodes') {
    group = pluginGroupName
    doLast {
        deployCordappHelper.listVNodes()
    }
}

tasks.register('listCPIs') {
    group = pluginImplGroupName
    doLast {
        deployCordappHelper.listCPIs()
    }
}


// Build CPI tasks

def buildCPIHelper = new BuildCPIHelper(projectContext)

tasks.register('projInit') {
    group = pluginImplGroupName
    doLast {
         mkdir devEnvWorkspace
    }
}

tasks.register("createGroupPolicy") {
    group = pluginImplGroupName
    dependsOn('projInit')
    doLast {

            def groupPolicyFName = new String("${devEnvWorkspace}/GroupPolicy.json")
            def devnetFName = new String("$rootDir/config/dev-net.json")
            File groupPolicyFile = new File(groupPolicyFName)
            File devnetFile = new File(devnetFName)
            if (!groupPolicyFile.exists() || groupPolicyFile.lastModified() < devnetFile.lastModified()) {
                def configX500Ids = projectUtils.getConfigX500Ids(projectContext.X500ConfigFile)

                println("createGroupPolicy: Creating a GroupPolicy")

                javaexec {
                    classpath = files("$cordaCliBinDir/corda-cli.jar")
                    jvmArgs = ["-Dpf4j.pluginsDir=$cordaCliBinDir/plugins/"]
                    standardOutput = new FileOutputStream(groupPolicyFName)
                    LinkedList<String> myArgs = new LinkedList<String>()
                    myArgs.add("mgm")
                    myArgs.add("groupPolicy")
                    configX500Ids.forEach {
                        myArgs.add("--name")
                        myArgs.add("$it")
                    }

                    myArgs.add("--endpoint-protocol=1")
                    myArgs.add("--endpoint=http://localhost:1080")
                    args = myArgs
                }

            } else {
                println("createPolicyTask: everything up to date; nothing to do.")
            }

    }
}


tasks.register("getNotaryServerCPB", Copy) {
    group = pluginImplGroupName
    from configurations.notaryServerCPB
    into cordaNotaryServerDir
}

tasks.register('createKeystore') {
    group = pluginImplGroupName
    dependsOn('projInit')
    doLast {
        File keystoreFile = new File(keystoreFName)
        if(!keystoreFile.exists()) {
            println('createKeystore: Create a keystore')
            exec {
                commandLine "${System.getProperty("java.home")}/bin/keytool", "-genkeypair",
                        "-alias", keystoreAlias,
                        "-keystore", keystoreFName,
                        "-storepass", "keystore password",
                        "-dname", "CN=CPI Example - My Signing Key, O=CorpOrgCorp, L=London, C=GB",
                        "-keyalg", "RSA",
                        "-storetype", "pkcs12",
                        "-validity", "4000"
            }
            // Need to add the default signing key to the keystore
            exec {
                commandLine "${System.getProperty("java.home")}/bin/keytool", "-importcert",
                        "-keystore", keystoreFName,
                        "-storepass", "keystore password",
                        "-noprompt",
                        "-alias", signingCertAlias,
                        "-file", signingCertFName
            }
            // keytool -exportcert -rfc -alias "signing key 1" -keystore signingkeys.pfx -storepass "keystore password" -file signingkey1.pem
            exec {
                commandLine "${System.getProperty("java.home")}/bin/keytool",
                        "-exportcert", "-rfc", "-alias", keystoreAlias,
                        "-keystore", keystoreFName,
                        "-storepass", "keystore password",
                        "-file", keystoreCertFName
            }
        }
        else {
            println('createKeystore:  keystore already created; nothing to do.')
        }

    }
}

tasks.register('buildCPIs') {
    group = pluginGroupName
    dependsOn('build', 'createGroupPolicy', 'createKeystore', 'getNotaryServerCPB')

    doLast{
        // Application CPI
        def appCpiFile = buildDir.toString() + "/" + project.archivesBaseName + "-" + project.version + ".cpi"
        delete { delete appCpiFile }
        File srcDir
        srcDir = file('build/libs')

        // Create a file collection using a closure
        def appCollection = layout.files { srcDir.listFiles() }
        def appCpbs = appCollection.filter { it.getName().endsWith(".cpb") }

        println("appCpbs:")
        appCpbs.forEach {
            println(it)
        }

        javaexec {
            classpath = files("$cordaCliBinDir/corda-cli.jar")
            jvmArgs = ["-Dpf4j.pluginsDir=$cordaCliBinDir/plugins/"]
            args = ['package', 'create-cpi',
                    '--cpb', appCpbs.singleFile.absolutePath,
                    '--group-policy', "${devEnvWorkspace}/GroupPolicy.json",
                    '--cpi-name', appCpiName,
                    '--cpi-version', project.version,
                    '--file', appCpiFile,
                    '--keystore', "${devEnvWorkspace}/signingkeys.pfx",
                    '--storepass', 'keystore password',
                    '--key', 'my-signing-key' ]
        }

        // Notary CPI
        def notaryCpiFile = buildDir.toString() + "/" +
                notaryCpiName.replace(' ', '-').toLowerCase() + "-" + project.version + ".cpi"
        delete { delete notaryCpiFile }

        // Create a file collection using a closure
        def notaryCpbs = layout
                .files { file(cordaNotaryServerDir).listFiles() }
                .filter { it.getName().endsWith(".cpb") }

        println("notaryCpbs:")
        notaryCpbs.forEach {
            println(it)
        }
        javaexec {
            classpath = files("$cordaCliBinDir/corda-cli.jar")
            jvmArgs = ["-Dpf4j.pluginsDir=$cordaCliBinDir/plugins/"]
            args = ['package', 'create-cpi',
                    '--cpb', notaryCpbs.singleFile.absolutePath,
                    '--group-policy', "${devEnvWorkspace}/GroupPolicy.json",
                    '--cpi-name', notaryCpiName,
                    '--cpi-version', project.version,
                    '--file', notaryCpiFile,
                    '--keystore', "${devEnvWorkspace}/signingkeys.pfx",
                    '--storepass', 'keystore password',
                    '--key', 'my-signing-key' ]
        }
    }

}



// deploy CPI tasks

tasks.register("deployCPIs") {
    group = pluginImplGroupName
    dependsOn('buildCPIs')
    doLast {
        deployCordappHelper.uploadCertificate(signingCertAlias, signingCertFName)
        deployCordappHelper.uploadCertificate(keystoreAlias, keystoreCertFName)
        deployCordappHelper.deployCPI("${buildDir}/${project.archivesBaseName}-${project.version}.cpi", appCpiName, project.version)
        deployCordappHelper.deployCPI(
                "${buildDir}/${notaryCpiName.replace(' ', '-').toLowerCase()}-${project.version}.cpi",
                notaryCpiName,
                project.version,
                "-NotaryServer")
    }
}

tasks.register("createAndRegVNodes") {
    group = pluginImplGroupName
    dependsOn('deployCPIs')
    doLast {
        deployCordappHelper.createAndRegVNodes()
    }
}


// Empty task, just acts as the Task user entry point task.
tasks.register('deployCordapp') {
    group = pluginGroupName
    dependsOn("createAndRegVNodes")
}


